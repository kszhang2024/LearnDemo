1)  <program> → {<declaration_list><statement_list>}
2)  <declaration_list> → <declaration_list><declaration_stat> | ε

改写为：
      <declaration_list> → <declaration_stat><declaration_list> | ε

3)  <declaration_stat> → int ID;
4)  <statement_list> → <statement_list><statement> | ε

改写为：
        <statement_list>→<statement><statement_list>| ε

5)  <statement> → <if_stat>|<while_stat>|<for_stat>|<read_stat>|<write_stat>| <compound_stat> | <assignment_stat> | ;
6)  <if_stat> → if (<bool_expression>) <statement>| if (<bool_expression>) <statement> else <statement>
提取左公因子：考虑是否满足条件3，若不满足可考虑超前一个单词进行判断。
不满足

改造后： 
 <if_stat> → if (<bool_expression>) <statement><if_other>
 <if_other> → else <statement> | ε

7) <while_stat> → while (<bool_expression>)<statement>
8) <for_stat> → for(<assignment_expression>;<bool_expression>;<assignment_expression>)<statement>
9) <write_stat> → write <arithmetic_expression>;
10) <read_stat> → read ID; 
11) <compound_stat> → {<statement_list>}
12) <assignment_expression> → ID=<arithmetic_expression>
13) <assignment_stat> → <assignment_expression>;
14) <bool_expression> →
<arithmetic_expression> >  <arithmetic_expression>
|<arithmetic_expression> <  <arithmetic_expression>
|<arithmetic_expression> >= <arithmetic_expression>
|<arithmetic_expression> <= <arithmetic_expression>
|<arithmetic_expression> == <arithmetic_expression>
|<arithmetic_expression> != <arithmetic_expression>

提取左公因子：
<bool_expression> → <arithmetic_expression><expression_other>
<expression_other> → 
>  <arithmetic_expression>
| <  <arithmetic_expression>
| >= <arithmetic_expression>
| <= <arithmetic_expression>
| == <arithmetic_expression>
| != <arithmetic_expression>

15) <arithmetic_expression> → <arithmetic_expression>+<term> | <arithmetic_expression>-<term> | <term> 

    左递归改右递归：
	<arithmetic_expression> → <term><arithmetic_expression1>
	<arithmetic_expression1> → +<term><arithmetic_expression1> | -<term><arithmetic_expression1> | ε

16) <term> → <term>*<factor> | <term>/<factor> | <factor>

    左递归改右递归：
	<term> → <factor><term1>
	<term1> → *<factor><term1> | /<factor><term1> | ε

17) <factor> → (<arithmetic_expression>) | ID | NUM
